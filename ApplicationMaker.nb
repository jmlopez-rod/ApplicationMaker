Notebook[{
Cell[BoxData[
 RowBox[{
  RowBox[{"(*", " ", 
   RowBox[{":", 
    RowBox[{"Title", ":", " ", "ApplicationMaker"}]}], " ", "*)"}], " ", "\n", 
  RowBox[{"(*", " ", 
   RowBox[{":", 
    RowBox[{"Author", ":", " ", "jmlopez"}]}], " ", "*)"}], "\n", 
  RowBox[{"(*", " ", 
   RowBox[{":", 
    RowBox[{"Email", ":", " ", 
     RowBox[{"jmlopez", ".", 
      RowBox[{"rod", "@", "gmail"}], ".", "com"}]}]}], " ", "*)"}], "\n", 
  RowBox[{"(*", " ", 
   RowBox[{":", 
    RowBox[{"Summary", ":", " ", 
     RowBox[{
     "Package", " ", "provides", " ", "functions", " ", "to", " ", "create", 
      " ", "directory", " ", "tree", " ", "and", " ", "to", " ", "export", 
      "\n", "\t\t\t ", "the", " ", 
      RowBox[{"application", "."}]}]}]}], " ", "*)"}], "\n", 
  RowBox[{"(*", " ", 
   RowBox[{":", 
    RowBox[{"Context", ":", " ", "ApplicationMaker`ApplicationMaker`"}]}], 
   " ", "*)"}], "\n", 
  RowBox[{"(*", " ", 
   RowBox[{":", 
    RowBox[{"Package", " ", 
     RowBox[{"version", ":", " ", "1.0"}]}]}], " ", "*)"}], "\n", 
  RowBox[{"(*", " ", 
   RowBox[{":", 
    RowBox[{"History", ":", "  ", 
     RowBox[{"Version", " ", "1.0", " ", "July", " ", "09", " ", "2011"}]}]}],
    " ", "*)"}], "\n", 
  RowBox[{"(*", " ", 
   RowBox[{":", 
    RowBox[{"Mathematica", " ", 
     RowBox[{"version", ":", " ", 
      RowBox[{
      "8.0", " ", "for", " ", "Mac", " ", "OS", " ", "X", " ", "x86", " ", 
       RowBox[{"(", 
        RowBox[{"64", "-", "bit"}], ")"}], " ", 
       RowBox[{"(", 
        RowBox[{
         RowBox[{"February", " ", "23"}], ",", " ", "2011"}], ")"}]}]}]}]}], 
   " ", "*)"}], "\n", 
  RowBox[{"(*", " ", 
   RowBox[{":", 
    RowBox[{"Discussion", ":", " ", 
     RowBox[{
     "The", " ", "function", " ", "NewApplication", " ", "creates", " ", "a", 
      " ", "directory", " ", "tree", " ", "at", " ", "a", "\n", "\t\t\t\t", 
      "specified", " ", "location", " ", "for", " ", "a", " ", "new", " ", 
      RowBox[{"application", ".", " ", "The"}], " ", "DeployApplication", 
      "\n", "\t\t\t\t", "takes", " ", "all", " ", "the", " ", "m", " ", 
      "files", " ", "and", " ", "documentation", " ", "notebooks", " ", "in", 
      " ", "the", "\n", "\t\t\t\t", "application", " ", "and", " ", "puts", 
      " ", "them", " ", "in", " ", "an", " ", "specified", " ", "location", 
      " ", "for", " ", "\n", "\t\t\t\t", 
      RowBox[{"distribution", "."}]}]}]}], " ", "*)"}]}]], "Code"],

Cell[CellGroupData[{

Cell["Initialization", "Section"],

Cell[CellGroupData[{

Cell["Public Context", "Subsection"],

Cell[BoxData[
 RowBox[{
  RowBox[{
  "BeginPackage", "[", "\"\<ApplicationMaker`ApplicationMaker`\>\"", "]"}], 
  ";"}]], "Code"],

Cell[BoxData[{
 RowBox[{
  RowBox[{"Unprotect", "[", 
   RowBox[{
   "NewApplication", ",", " ", "BuildApplication", ",", " ", 
    "DeployApplication"}], "]"}], ";"}], "\n", 
 RowBox[{
  RowBox[{"ClearAll", "[", 
   RowBox[{
   "NewApplication", ",", " ", "BuildApplication", ",", " ", 
    "DeployApplication"}], "]"}], ";"}]}], "Code"]
}, Closed]],

Cell[CellGroupData[{

Cell["Messages", "Subsection"],

Cell[CellGroupData[{

Cell["Usage Messages", "Subsubsection"],

Cell[BoxData[
 RowBox[{
  RowBox[{
   RowBox[{"$ApplicationsDirectory", "::", "usage"}], " ", "=", " ", 
   "\"\<Default directory for Applications developing and deploying.\>\""}], 
  ";"}]], "Code"],

Cell[BoxData[
 RowBox[{
  RowBox[{
   RowBox[{"NewApplication", "::", "usage"}], " ", "=", " ", 
   "\[IndentingNewLine]", "\"\<NewApplication[\!\(\*
StyleBox[\"appName\", \"TI\"]\)] creates a directory named \!\(\*
StyleBox[\"appName\", \"TI\"]\) in \!\(\*
StyleBox[\"$UserBaseDirectory\", \"Program\"]\)\!\(\*
StyleBox[\"/\", \"Program\"]\)\!\(\*
StyleBox[\"Applications\", \"Program\"]\)\!\(\*
StyleBox[\"/\", \"Program\"]\) and subdirectories required to make an \
application with documentation.\>\""}], ";"}]], "Code"],

Cell[BoxData[
 RowBox[{
  RowBox[{
   RowBox[{"BuildApplication", "::", "usage"}], " ", "=", 
   "\[IndentingNewLine]", "\"\<BuildApplication[\!\(\*
StyleBox[\"appName\", \"TI\"]\)] creates the documentation files and the \
index for the documentation center.\>\""}], ";"}]], "Code"],

Cell[BoxData[
 RowBox[{
  RowBox[{
   RowBox[{"DeployApplication", "::", "usage"}], " ", "=", 
   "\[IndentingNewLine]", "\"\<DeployApplication[\!\(\*
StyleBox[\"appName\", \"TI\"]\), \!\(\*
StyleBox[\"destDir\", \"TI\"]\)] copies the m files and documentation of your \
application into \!\(\*
StyleBox[\"destDir\", \"TI\"]\)\>\""}], ";"}]], "Code"]
}, Open  ]],

Cell[CellGroupData[{

Cell["Error Messages", "Subsubsection"],

Cell[BoxData[
 RowBox[{
  RowBox[{
   RowBox[{"NewApplication", "::", "argerr"}], " ", "=", "\n", 
   "\"\<String specifying the application name was expected.\>\""}], 
  ";"}]], "Code"],

Cell[BoxData[
 RowBox[{
  RowBox[{
   RowBox[{"BuildApplication", "::", "argerr"}], " ", "=", "\n", 
   "\"\<String specifying the application name was expected.\>\""}], 
  ";"}]], "Code"],

Cell[BoxData[
 RowBox[{
  RowBox[{
   RowBox[{"BuildApplication", "::", "nodir"}], " ", "=", "\n", 
   "\"\<There is no application `1` in `2`. To create a new application use \
NewApplication\>\""}], ";"}]], "Code"],

Cell[BoxData[
 RowBox[{
  RowBox[{
   RowBox[{"DeployApplication", "::", "argerr"}], " ", "=", "\n", 
   "\"\<Strings specifying the application name and the destination were \
expected.\>\""}], ";"}]], "Code"],

Cell[BoxData[
 RowBox[{
  RowBox[{
   RowBox[{"DeployApplication", "::", "nodir"}], " ", "=", "\n", 
   "\"\<There is no application `1` in `2`. To create a new application use \
NewApplication\>\""}], ";"}]], "Code"]
}, Open  ]]
}, Closed]],

Cell[CellGroupData[{

Cell["Private Context", "Subsection"],

Cell[BoxData[
 RowBox[{
  RowBox[{"Begin", "[", "\"\<`Private`\>\"", "]"}], ";"}]], "Code"]
}, Closed]]
}, Closed]],

Cell[CellGroupData[{

Cell["Implementation", "Section"],

Cell[CellGroupData[{

Cell["Dependencies", "Subsection"],

Cell[CellGroupData[{

Cell["One constant", "Subsubsection"],

Cell[BoxData[
 RowBox[{
  RowBox[{"$ApplicationsDirectory", " ", "=", " ", 
   RowBox[{"FileNameJoin", "@", 
    RowBox[{"{", 
     RowBox[{"$UserBaseDirectory", ",", " ", "\"\<Applications\>\""}], 
     "}"}]}]}], ";"}]], "Code"]
}, Closed]],

Cell[CellGroupData[{

Cell["Subsidiary string operations", "Subsubsection"],

Cell[BoxData[
 RowBox[{
  RowBox[{"spaceRiffle", " ", "=", " ", 
   RowBox[{
    RowBox[{"Riffle", "[", 
     RowBox[{
      RowBox[{"Flatten", "@", 
       RowBox[{"{", "##", "}"}]}], ",", " ", "\"\< \>\""}], "]"}], "&"}]}], 
  ";"}]], "Code"],

Cell[BoxData[
 RowBox[{
  RowBox[{"spaceRiffledString", " ", "=", " ", 
   RowBox[{"Composition", "[", 
    RowBox[{"StringJoin", ",", " ", "spaceRiffle"}], "]"}]}], ";"}]], "Code"],

Cell[BoxData[
 RowBox[{
  RowBox[{"prefixStringViaSpace", "@", "s_String"}], " ", ":=", " ", 
  RowBox[{
   RowBox[{"spaceRiffledString", "@", 
    RowBox[{"{", 
     RowBox[{"s", ",", " ", "#"}], "}"}]}], "&"}]}]], "Code"],

Cell["I hope the following will become obsolete:", "Text"],

Cell[BoxData[
 RowBox[{
  RowBox[{"fileBaseNameWithoutFirstThreeChars", "@", "path_"}], " ", ":=", 
  "\n", 
  RowBox[{"StringDrop", "[", 
   RowBox[{
    RowBox[{"FileBaseName", "@", "path"}], ",", " ", "3"}], "]"}]}]], "Code"]
}, Closed]],

Cell[CellGroupData[{

Cell["Messages styles", "Subsubsection"],

Cell[BoxData[{
 RowBox[{
  RowBox[{"boldMsg", " ", "=", " ", 
   RowBox[{
    RowBox[{"Style", "[", 
     RowBox[{"#", ",", " ", "\"\<MSG\>\"", ",", " ", "Bold"}], "]"}], "&"}]}],
   ";"}], "\n", 
 RowBox[{
  RowBox[{"grayMsg", " ", "=", " ", 
   RowBox[{
    RowBox[{"Style", "[", 
     RowBox[{"#", ",", " ", "\"\<MSG\>\"", ",", " ", "Gray"}], "]"}], "&"}]}],
   ";"}], "\n", 
 RowBox[{
  RowBox[{"blueMsg", " ", "=", " ", 
   RowBox[{
    RowBox[{"Style", "[", 
     RowBox[{"#", ",", " ", "\"\<MSG\>\"", ",", " ", "Blue"}], "]"}], "&"}]}],
   ";"}], "\n", 
 RowBox[{
  RowBox[{"plainMsg", " ", "=", " ", 
   RowBox[{
    RowBox[{"Style", "[", 
     RowBox[{"#", ",", " ", "\"\<MSG\>\""}], "]"}], "&"}]}], ";"}]}], "Code"]
}, Closed]],

Cell[CellGroupData[{

Cell["Directory tree creation", "Subsubsection"],

Cell["\<\
The original commented directory creation (hopefully, obsolete already):\
\>", "Text"],

Cell[BoxData[
 RowBox[{
  RowBox[{"createDirWithCommentPrinted", " ", "=", "\n", 
   RowBox[{
    RowBox[{"If", "[", 
     RowBox[{
      RowBox[{"DirectoryQ", "@", "#"}], "\n", ",", "  ", 
      RowBox[{"Print", "[", 
       RowBox[{
        RowBox[{"grayMsg", "@", "\"\<Directory exists: \>\""}], ",", " ", 
        RowBox[{"boldMsg", "@", "#"}]}], "]"}], "\n", ",", "  ", 
      RowBox[{
       RowBox[{"CreateDirectory", "@", "#"}], ";", "\n", "   ", 
       RowBox[{"Print", "[", 
        RowBox[{
         RowBox[{"blueMsg", "@", "\"\<Directory created: \>\""}], ",", " ", 
         RowBox[{"boldMsg", "@", "#"}]}], "]"}]}]}], "]"}], "&"}]}], 
  ";"}]], "Code"],

Cell["Modified commented directory creation:", "Text"],

Cell[BoxData[
 RowBox[{
  RowBox[{"createDirIfPossible", " ", "=", "\n", 
   RowBox[{
    RowBox[{"Block", "[", 
     RowBox[{
      RowBox[{"{", "CreateDirectory", "}"}], ",", "\n", 
      RowBox[{"If", "[", 
       RowBox[{
        RowBox[{"DirectoryQ", "@", "#"}], ",", "\n", 
        RowBox[{
         RowBox[{"Sequence", " ", "@@", " ", 
          RowBox[{"Transpose", " ", "@", "\n", 
           RowBox[{"{", " ", 
            RowBox[{
             RowBox[{
              RowBox[{"prefixStringViaSpace", "@", "\"\<Directory\>\""}], " ",
               "/@", "\n", "  ", 
              RowBox[{"{", " ", 
               RowBox[{
               "\"\<exists\>\"", " ", ",", "     ", "\"\<created\>\""}], 
               "      ", "}"}]}], "\n", ",", " ", 
             RowBox[{"{", "    ", 
              RowBox[{"#", "     ", ",", " ", 
               RowBox[{"CreateDirectory", "@", "#"}]}], " ", "}"}]}], " ", 
            "}"}]}]}], " ", "//", " ", "Evaluate"}]}], "]"}]}], "]"}], 
    "&"}]}], ";"}]], "Code"],

Cell[BoxData[{
 RowBox[{
  RowBox[{
   RowBox[{"createDirTree", "[", 
    RowBox[{"address___", ",", " ", 
     RowBox[{"dir_String", "@", "subDirs___"}]}], "]"}], " ", ":=", "\n", 
   RowBox[{"Map", "[", 
    RowBox[{
     RowBox[{
      RowBox[{"createDirTree", "[", 
       RowBox[{"address", ",", " ", "dir", ",", " ", "#"}], "]"}], "&"}], ",",
      " ", 
     RowBox[{"dir", "@", "subDirs"}], ",", " ", 
     RowBox[{"Heads", " ", "->", " ", "True"}]}], "]"}]}], "\n"}], "\n", 
 RowBox[{
  RowBox[{"createDirTree", "[", 
   RowBox[{"address___", ",", " ", 
    RowBox[{"_", "?", "AtomQ"}]}], "]"}], " ", ":=", "\n", 
  RowBox[{"createDirIfPossible", "@", 
   RowBox[{"FileNameJoin", "@", 
    RowBox[{"{", "address", "}"}]}]}]}]}], "Code"],

Cell["\<\
Directories are represented as \"dirName\"[], so\[Ellipsis]\
\>", "Text"],

Cell[BoxData[
 RowBox[{
  RowBox[{"openAtoms", " ", "=", " ", 
   RowBox[{
    RowBox[{"Replace", "[", 
     RowBox[{"#", ",", " ", 
      RowBox[{
       RowBox[{"a_", "?", "AtomQ"}], " ", ":>", " ", 
       RowBox[{"a", "[", "]"}]}], ",", " ", 
      RowBox[{"{", 
       RowBox[{"1", ",", " ", "\[Infinity]"}], "}"}]}], "]"}], "&"}]}], 
  ";"}]], "Code"],

Cell[BoxData[
 RowBox[{
  RowBox[{"stringTreeQ", "@", "expr_"}], " ", ":=", "\n", 
  RowBox[{"MatchQ", "[", "\n", "  ", 
   RowBox[{
    RowBox[{"Replace", "[", "\n", "    ", 
     RowBox[{
      RowBox[{"Cases", "[", 
       RowBox[{"expr", ",", " ", 
        RowBox[{
         RowBox[{"_", "?", "AtomQ"}], "|", 
         RowBox[{
          RowBox[{"_", "?", "AtomQ"}], "[", "]"}]}], "\n", "    ", ",", " ", 
        RowBox[{"{", 
         RowBox[{"0", ",", " ", "\[Infinity]"}], "}"}], ",", " ", 
        RowBox[{"Heads", " ", "->", " ", "True"}]}], "]"}], "\n", "  ", ",", 
      " ", 
      RowBox[{
       RowBox[{"s_String", "[", "]"}], " ", ":>", " ", "s"}], ",", " ", 
      RowBox[{"{", "1", "}"}]}], "]"}], "\n", ",", " ", 
    RowBox[{"{", "__String", "}"}]}], "]"}]}]], "Code"],

Cell[BoxData[{
 RowBox[{
  RowBox[{
   RowBox[{
    RowBox[{"Options", "@", "CreateDirectoryTree"}], " ", "=", " ", 
    RowBox[{"{", 
     RowBox[{"\"\<Output Form\>\"", " ", "->", " ", "OpenerView"}], "}"}]}], 
   ";"}], "\n"}], "\n", 
 RowBox[{
  RowBox[{"CreateDirectoryTree", "[", " ", 
   RowBox[{
    RowBox[{"tree_", "?", "stringTreeQ"}], "\n", "                   ", ",", 
    " ", 
    RowBox[{"root", ":", 
     RowBox[{"_String", ":", "$HomeDirectory"}]}], "\n", 
    "                   ", ",", " ", 
    RowBox[{"OptionsPattern", "[", "]"}]}], "]"}], " ", ":=", "\n", 
  RowBox[{
   RowBox[{"createDirTree", "@", 
    RowBox[{"openAtoms", "@", 
     RowBox[{"root", "@", "tree"}]}]}], " ", "//.", "\n", 
   RowBox[{"If", "[", 
    RowBox[{
     RowBox[{
      RowBox[{"OptionValue", "@", "\"\<Output Form\>\""}], " ", "===", " ", 
      "OpenerView"}], "\n", ",", "  ", 
     RowBox[{"{", " ", 
      RowBox[{
       RowBox[{
        RowBox[{
         RowBox[{"{", 
          RowBox[{"msg_String", ",", " ", "addr_String"}], "}"}], "[", "]"}], 
        " ", ":>", " ", 
        RowBox[{
        "msg", " ", "<>", " ", "\"\<: \>\"", " ", "<>", " ", "addr"}]}], "\n",
        "   ", ",", " ", 
       RowBox[{
        RowBox[{
         RowBox[{"{", 
          RowBox[{"msg_String", ",", " ", "addr_String"}], "}"}], "@", 
         "sub__"}], " ", ":>", "\n", "     ", 
        RowBox[{"OpenerView", "[", 
         RowBox[{
          RowBox[{"{", 
           RowBox[{
            RowBox[{
             RowBox[{"{", 
              RowBox[{"msg", ",", " ", "addr"}], "}"}], "[", "]"}], ",", " ", 
            RowBox[{"Column", "@", 
             RowBox[{"{", "sub", "}"}]}]}], "}"}], ",", " ", "False"}], 
         "]"}]}]}], "}"}], "\n", ",", "  ", 
     RowBox[{"{", "}"}]}], "]"}]}]}]}], "Code"]
}, Closed]],

Cell[CellGroupData[{

Cell["Delete multiple files in a directory tree", "Subsubsection"],

Cell[BoxData[{
 RowBox[{
  RowBox[{
   RowBox[{"deleteFiles", "[", "\n", "  ", 
    RowBox[{
     RowBox[{"dir_String", "@", "sub__"}], "\n", ",", " ", 
     RowBox[{"root", ":", 
      RowBox[{"_String", ":", "$HomeDirectory"}]}]}], "]"}], " ", ":=", "\n", 
   RowBox[{"With", "[", 
    RowBox[{
     RowBox[{"{", 
      RowBox[{"newroot", " ", "=", " ", 
       RowBox[{"FileNameJoin", "@", 
        RowBox[{"{", 
         RowBox[{"root", ",", " ", "dir"}], "}"}]}]}], "}"}], "\n", ",", " ", 
     RowBox[{"OpenerView", " ", "@", "\n", "  ", 
      RowBox[{"{", " ", 
       RowBox[{"newroot", "\n", "  ", ",", " ", 
        RowBox[{
         RowBox[{
          RowBox[{
           RowBox[{"deleteFiles", "[", 
            RowBox[{"#", ",", " ", "newroot"}], "]"}], "&"}], " ", "/@", " ", 
          RowBox[{"{", "sub", "}"}]}], " ", "//", " ", "Column"}]}], " ", 
       "}"}]}]}], "]"}]}], "\n"}], "\n", 
 RowBox[{
  RowBox[{"deleteFiles", "[", "\n", "  ", 
   RowBox[{"fileNamePattern_String", "\n", ",", " ", 
    RowBox[{"root", ":", 
     RowBox[{"_String", ":", "$HomeDirectory"}]}]}], "]"}], " ", ":=", "\n", 
  RowBox[{"With", "[", "\n", "  ", 
   RowBox[{
    RowBox[{"{", " ", 
     RowBox[{"listOfFilesToDelete", " ", "=", "\n", "    ", 
      RowBox[{"FileNames", " ", "@", " ", 
       RowBox[{"FileNameJoin", " ", "@", " ", 
        RowBox[{"{", 
         RowBox[{"root", ",", " ", "fileNamePattern"}], "}"}]}]}]}], "}"}], 
    "\n", ",", " ", 
    RowBox[{"Column", " ", "@", "\n", "  ", 
     RowBox[{"{", " ", 
      RowBox[{"\"\<Files deleted:\>\"", "\n", "  ", ",", " ", 
       RowBox[{"Replace", "[", "\n", "      ", 
        RowBox[{
         RowBox[{
          RowBox[{
           RowBox[{"(", 
            RowBox[{
             RowBox[{"DeleteFile", "@", "#"}], ";", " ", 
             RowBox[{"Column", "@", "#"}]}], ")"}], "&"}], " ", "@", " ", 
          "listOfFilesToDelete"}], "\n", "    ", ",", " ", 
         RowBox[{
          RowBox[{"Column", "@", 
           RowBox[{"{", "}"}]}], " ", "->", " ", "\"\<(None)\>\""}]}], 
        "]"}]}], "}"}]}]}], "]"}]}]}], "Code"]
}, Closed]],

Cell[CellGroupData[{

Cell["listOfFiles", "Subsubsection"],

Cell[BoxData[{
 RowBox[{
  RowBox[{
   RowBox[{"listOfFiles", "[", "\n", "  ", 
    RowBox[{
     RowBox[{"dir_String", "@", "sub__"}], "\n", ",", " ", 
     RowBox[{"root", ":", 
      RowBox[{"_String", ":", "$HomeDirectory"}]}]}], "]"}], " ", ":=", "\n", 
   RowBox[{"With", "[", 
    RowBox[{
     RowBox[{"{", 
      RowBox[{"newroot", " ", "=", " ", 
       RowBox[{"FileNameJoin", "@", 
        RowBox[{"{", 
         RowBox[{"root", ",", " ", "dir"}], "}"}]}]}], "}"}], "\n", ",", " ", 
     RowBox[{
      RowBox[{
       RowBox[{
        RowBox[{"listOfFiles", "[", 
         RowBox[{"#", ",", " ", "newroot"}], "]"}], "&"}], " ", "/@", " ", 
       RowBox[{"{", "sub", "}"}]}], " ", "//", " ", "Flatten"}]}], "]"}]}], 
  "\n"}], "\n", 
 RowBox[{
  RowBox[{"listOfFiles", "[", "\n", "  ", 
   RowBox[{"fileNamePattern_String", "\n", ",", " ", 
    RowBox[{"root", ":", 
     RowBox[{"_String", ":", "$HomeDirectory"}]}]}], "]"}], " ", ":=", "\n", 
  RowBox[{"FileNames", " ", "@", " ", 
   RowBox[{"FileNameJoin", " ", "@", " ", 
    RowBox[{"{", 
     RowBox[{"root", ",", " ", "fileNamePattern"}], "}"}]}]}]}]}], "Code"]
}, Open  ]]
}, Open  ]],

Cell[CellGroupData[{

Cell["Main functions", "Subsection"],

Cell[CellGroupData[{

Cell["NewApplication", "Subsubsection"],

Cell[BoxData[{
 RowBox[{
  RowBox[{
   RowBox[{"NewApplication", "[", "\n", "  ", 
    RowBox[{"appName_String", "\n", ",", " ", 
     RowBox[{"appsDir_String:", " ", "$ApplicationsDirectory"}]}], "]"}], " ",
    ":=", "\n", 
   RowBox[{"CreateDirectoryTree", "[", "\n", "  ", 
    RowBox[{
     RowBox[{"appName", "[", "\n", "    ", 
      RowBox[{"\"\<Kernel\>\"", "\n", "  ", ",", " ", 
       RowBox[{"\"\<Documentation\>\"", "[", "\n", "      ", 
        RowBox[{"\"\<English\>\"", "[", "\n", "        ", 
         RowBox[{
         "\"\<Guides\>\"", "\n", "      ", ",", " ", "\"\<Tutorials\>\"", 
          "\n", "      ", ",", " ", 
          RowBox[{"\"\<ReferencePages\>\"", "@", "\"\<Symbols\>\""}]}], "]"}],
         "]"}]}], "]"}], "\n", ",", " ", "appsDir"}], "]"}]}], "\n"}], "\n", 
 RowBox[{
  RowBox[{"NewApplication", "@", "smthElse___"}], " ", ":=", " ", 
  RowBox[{"(", 
   RowBox[{
    RowBox[{"Message", "[", 
     RowBox[{"NewApplication", "::", "argerr"}], "]"}], ";", " ", "$Failed"}],
    ")"}]}]}], "Code"]
}, Closed]],

Cell[CellGroupData[{

Cell["ChangeNotebookSettings", "Subsubsection"],

Cell[BoxData[
 RowBox[{
  RowBox[{"ChangeNotebookSettings", "[", 
   RowBox[{
   "path_", ",", " ", "index_", ",", " ", "header_", ",", " ", "footer_"}], 
   "]"}], " ", ":=", "\n", 
  RowBox[{"With", "[", "\n", "  ", 
   RowBox[{
    RowBox[{"{", " ", 
     RowBox[{
      RowBox[{"someCell", " ", "=", "\n", "    ", 
       RowBox[{"Cell", "[", "\n", "      ", 
        RowBox[{
         RowBox[{"TextData", " ", "@", "\n", "      ", 
          RowBox[{"{", " ", 
           RowBox[{
            RowBox[{"Cell", "[", 
             RowBox[{
              RowBox[{"TextData", "@", 
               RowBox[{"{", "header", "}"}]}], ",", " ", 
              "\"\<PageHeader\>\""}], "]"}], "\n", "      ", ",", " ", 
            RowBox[{"Cell", "[", 
             RowBox[{
              RowBox[{"TextData", "@", 
               RowBox[{"{", 
                RowBox[{"CounterBox", "@", "\"\<Page\>\""}], "}"}]}], ",", 
              " ", "\"\<PageNumber\>\""}], "]"}]}], " ", "}"}]}], "\n", 
         "    ", ",", " ", 
         RowBox[{"CellMargins", " ", "->", " ", 
          RowBox[{"{", 
           RowBox[{
            RowBox[{"{", 
             RowBox[{"Inherited", ",", " ", 
              RowBox[{"-", "29"}]}], "}"}], ",", " ", 
            RowBox[{"{", 
             RowBox[{"Inherited", ",", " ", "Inherited"}], "}"}]}], "}"}]}]}],
         "]"}]}], "\n", "  ", ",", " ", 
      RowBox[{"newpath", " ", "=", "\n", "    ", 
       RowBox[{"FileNameJoin", " ", "@", "\n", "    ", 
        RowBox[{"{", " ", 
         RowBox[{
          RowBox[{"DirectoryName", "@", "path"}], "\n", "    ", ",", " ", 
          RowBox[{"StringDrop", "[", 
           RowBox[{
            RowBox[{"FileNameTake", "@", "path"}], ",", " ", "3"}], "]"}]}], 
         " ", "}"}]}]}]}], "}"}], "\n", ",", " ", 
    RowBox[{"Module", "[", 
     RowBox[{
      RowBox[{"{", "nb", "}"}], ",", "\n", "    ", 
      RowBox[{
       RowBox[{"nb", " ", "=", " ", 
        RowBox[{"NotebookOpen", "@", "path"}]}], ";", "\n", "\n", "    ", 
       RowBox[{"NotebookSave", "[", 
        RowBox[{"nb", ",", " ", "newpath"}], "]"}], ";", "\n", "\n", "    ", 
       RowBox[{"SetOptions", "[", 
        RowBox[{"nb", "\n", "    ", ",", " ", 
         RowBox[{"Saveable", " ", "->", " ", "False"}], "\n", "    ", ",", 
         " ", 
         RowBox[{"StyleDefinitions", " ", "->", " ", 
          RowBox[{"FrontEnd`FileName", "[", 
           RowBox[{
            RowBox[{"{", "\"\<Wolfram\>\"", "}"}], ",", " ", 
            "\"\<Reference.nb\>\""}], "]"}]}], "\n", "    ", ",", " ", 
         RowBox[{"DockedCells", " ", "->", " ", 
          RowBox[{"FEPrivate`FrontEndResource", "[", 
           RowBox[{
           "\"\<FEExpressions\>\"", ",", " ", "\"\<HelpViewerToolbar\>\""}], 
           "]"}]}], "\n", "    ", ",", " ", 
         RowBox[{"PageFooters", " ", "->", "\n", "      ", 
          RowBox[{"{", " ", 
           RowBox[{
            RowBox[{"{", "                ", 
             RowBox[{
             "None", ",", "                  ", "None", ",", " ", "None"}], 
             "}"}], "\n", "      ", ",", " ", 
            RowBox[{"{", 
             RowBox[{
              RowBox[{"Cell", "[", 
               RowBox[{
                RowBox[{"TextData", "@", 
                 RowBox[{"{", "footer", "}"}]}], ",", " ", 
                "\"\<PageFooter\>\""}], "]"}], ",", " ", "None", ",", " ", 
              "None"}], "}"}]}], " ", "}"}]}], ",", "\n", "      ", 
         RowBox[{"PageHeaders", " ", "->", "\n", "      ", 
          RowBox[{"{", " ", 
           RowBox[{
            RowBox[{"{", 
             RowBox[{"None", ",", " ", "None", ",", "   ", "None"}], "  ", 
             "}"}], "\n", "      ", ",", " ", 
            RowBox[{"{", 
             RowBox[{"None", ",", " ", "None", ",", " ", "someCell"}], 
             "}"}]}], " ", "}"}]}], "\n", "    ", ",", " ", 
         RowBox[{
          RowBox[{"##", "&"}], "@@", 
          RowBox[{"Options", "[", 
           RowBox[{"nb", ",", " ", "WindowTitle"}], "]"}]}]}], "]"}], ";", 
       "\n", "    ", 
       RowBox[{"NotebookSave", "@", "nb"}], ";", "\n", "    ", 
       RowBox[{"NotebookClose", "@", "nb"}], ";", "\n", "    ", 
       RowBox[{"DocumentationSearch`AddDocumentationNotebook", "[", 
        RowBox[{"index", ",", " ", "newpath"}], "]"}], ";", "\n", "    ", 
       RowBox[{"fileBaseNameWithoutFirstThreeChars", "@", "path"}]}]}], 
     "]"}]}], "]"}]}]], "Code"]
}, Closed]],

Cell[CellGroupData[{

Cell["BuildApplication", "Subsubsection"],

Cell[BoxData[
 RowBox[{
  RowBox[{"packageFilesNames", "[", 
   RowBox[{"appName_String", "\n", ",", " ", 
    RowBox[{"appsDir_String:", "$ApplicationsDirectory"}]}], "]"}], " ", ":=",
   "\n", 
  RowBox[{"DeleteCases", "[", "\n", "  ", 
   RowBox[{
    RowBox[{"FileBaseName", " ", "/@", " ", 
     RowBox[{"FileNames", "@", 
      RowBox[{"FileNameJoin", "@", 
       RowBox[{"{", 
        RowBox[{"appsDir", ",", " ", "appName", ",", " ", "\"\<*.m\>\""}], 
        "}"}]}]}]}], "\n", ",", " ", "\"\<PacletInfo\>\""}], "]"}]}]], "Code"],

Cell[BoxData[
 RowBox[{
  RowBox[{"contextNames", "[", 
   RowBox[{"appName_String", "\n", ",", " ", 
    RowBox[{"appsDir_String:", "$ApplicationsDirectory"}]}], "]"}], " ", ":=",
   "\n", 
  RowBox[{
   RowBox[{
    RowBox[{
    "appName", " ", "<>", " ", "\"\<`\>\"", " ", "<>", " ", "#", " ", "<>", 
     " ", "\"\<`\>\""}], " ", "&"}], " ", "/@", "\n", 
   RowBox[{"packageFilesNames", "[", 
    RowBox[{"appName", ",", " ", "appsDir"}], "]"}]}]}]], "Code"],

Cell[BoxData[
 RowBox[{
  RowBox[{"englishResourcesList", "[", 
   RowBox[{"appName_String", "\n", ",", " ", 
    RowBox[{"appsDir_String:", "$ApplicationsDirectory"}]}], "]"}], " ", ":=",
   "\n", 
  RowBox[{
   RowBox[{"Composition", "[", "\n", "  ", 
    RowBox[{
     RowBox[{
      RowBox[{"FileNameJoin", "[", 
       RowBox[{"#", ",", " ", 
        RowBox[{"OperatingSystem", " ", "\[Rule]", " ", "\"\<Unix\>\""}]}], 
       "]"}], "&"}], "\n", ",", " ", 
     RowBox[{
      RowBox[{"Replace", "[", 
       RowBox[{"#", "\n", "  ", ",", " ", 
        RowBox[{
         RowBox[{"{", 
          RowBox[{
          "most___", ",", " ", "\"\<Documentation\>\"", ",", " ", 
           "\"\<English\>\"", ",", " ", "rest___", ",", " ", "last_"}], "}"}],
          " ", "\[RuleDelayed]", "\n", "    ", 
         RowBox[{"{", 
          RowBox[{"rest", ",", " ", 
           RowBox[{"StringReplace", "[", 
            RowBox[{
             RowBox[{"FileBaseName", "@", "last"}], "\n", "           ", ",", 
             " ", 
             RowBox[{
              RowBox[{"StartOfString", "~~", 
               RowBox[{"\"\<_\>\"", ".."}], "~~", "filename_"}], " ", 
              "\[RuleDelayed]", " ", "filename"}]}], "]"}]}], "}"}]}]}], 
       "]"}], "&"}], "\n", ",", " ", "FileNameSplit"}], "]"}], " ", "/@", 
   "\n", 
   RowBox[{"listOfFiles", "[", "\n", "  ", 
    RowBox[{
     RowBox[{"appName", "[", "\[IndentingNewLine]", "    ", 
      RowBox[{
       RowBox[{
        RowBox[{"\"\<Documentation\>\"", "[", "\n", "      ", 
         RowBox[{"\"\<English\>\"", "[", "\n", "        ", 
          RowBox[{
           RowBox[{"\"\<Guides\>\"", "@", "#"}], "\n", "      ", ",", " ", 
           RowBox[{"\"\<Tutorials\>\"", "@", "#"}], "\n", "      ", ",", " ", 
           RowBox[{"\"\<ReferencePages\>\"", "@", 
            RowBox[{"\"\<Symbols\>\"", "@", "#"}]}]}], "]"}], "]"}], "&"}], 
       " ", "@", " ", "\"\<___*.nb\>\""}], "]"}], "\n", ",", " ", "appsDir"}],
     "]"}]}]}]], "Code"],

Cell[BoxData[{
 RowBox[{
  RowBox[{
   RowBox[{"BuildApplication", "[", "\n", "  ", 
    RowBox[{"appName_String", "\n", ",", " ", 
     RowBox[{"version_String:", " ", "\"\<0.0.1\>\""}], "\n", ",", " ", 
     RowBox[{"header_String:", " ", "\"\<\>\""}], "\n", ",", " ", 
     RowBox[{"footer_String:", " ", "\"\<\>\""}], "\n", ",", " ", 
     RowBox[{"appsDir_String:", " ", "$ApplicationsDirectory"}]}], "]"}], " ",
    ":=", "\n", 
   RowBox[{"With", "[", 
    RowBox[{
     RowBox[{"{", 
      RowBox[{"engResources", " ", "=", " ", 
       RowBox[{"englishResourcesList", "[", 
        RowBox[{"appName", ",", " ", "appsDir"}], "]"}]}], "}"}], ",", "\n", 
     RowBox[{"Module", "[", "\n", "  ", 
      RowBox[{
       RowBox[{"{", 
        RowBox[{
        "appNameDir", ",", " ", "indexDir", ",", " ", "tmpPath", ",", " ", 
         "files", ",", " ", "index"}], "}"}], ",", "\n", "\n", "  ", 
       RowBox[{
        RowBox[{"appNameDir", " ", "=", " ", 
         RowBox[{"FileNameJoin", "@", 
          RowBox[{"{", 
           RowBox[{"appsDir", ",", " ", "appName"}], "}"}]}]}], ";", "\n", 
        "  ", 
        RowBox[{"indexDir", " ", "=", " ", 
         RowBox[{"FileNameJoin", "@", 
          RowBox[{"{", 
           RowBox[{
           "appNameDir", ",", " ", "\"\<Documentation\>\"", ",", " ", 
            "\"\<English\>\"", ",", " ", "\"\<Index\>\""}], "}"}]}]}], ";", 
        "\n", "\n", "  ", 
        RowBox[{"If", "[", 
         RowBox[{
          RowBox[{"!", 
           RowBox[{"DirectoryQ", "@", "appNameDir"}]}], "\n", "  ", ",", "  ", 
          RowBox[{
           RowBox[{"Message", "[", 
            RowBox[{
             RowBox[{"BuildApplication", "::", "nodir"}], ",", " ", "appName",
              ",", " ", "appsDir"}], "]"}], ";", " ", 
           RowBox[{"Return", "@", "$Failed"}]}]}], "]"}], ";", "\n", "\n", 
        "  ", 
        RowBox[{"If", "[", 
         RowBox[{
          RowBox[{
           RowBox[{"FileNames", "@", "indexDir"}], " ", "!=", " ", 
           RowBox[{"{", "}"}]}], "\n", "  ", ",", "  ", 
          RowBox[{"DeleteDirectory", "[", 
           RowBox[{"indexDir", ",", " ", 
            RowBox[{"DeleteContents", " ", "->", " ", "True"}]}], "]"}]}], 
         "]"}], ";", "\n", "\n", "  ", 
        RowBox[{"index", " ", "=", " ", 
         RowBox[{
         "DocumentationSearch`NewDocumentationNotebookIndexer", "@", 
          "indexDir"}]}], ";", "\n", "\n", "  ", 
        RowBox[{"Block", "[", "\n", "    ", 
         RowBox[{
          RowBox[{"{", " ", 
           RowBox[{
            RowBox[{"Paclet", " ", "=", " ", "PacletManager`Paclet"}], "\n", 
            "    ", ",", " ", 
            RowBox[{"Name", " ", "=", " ", "Global`Name"}], "\n", "    ", ",",
             " ", 
            RowBox[{
            "MathematicaVersion", " ", "=", " ", 
             "Global`MathematicaVersion"}], "\n", "    ", ",", " ", 
            RowBox[{"Extensions", " ", "=", " ", "Global`Extensions"}], "\n", 
            "    ", ",", " ", 
            RowBox[{"LinkBase", " ", "=", " ", "Global`LinkBase"}], "\n", 
            "    ", ",", " ", 
            RowBox[{"Resources", " ", "=", " ", "Global`Resources"}]}], "}"}],
           "\n", "  ", ",", " ", 
          RowBox[{"Export", "[", 
           RowBox[{
            RowBox[{"FileNameJoin", "@", 
             RowBox[{"{", 
              RowBox[{"appNameDir", ",", " ", "\"\<PacletInfo.m\>\""}], 
              "}"}]}], "\n", "    ", ",", " ", 
            RowBox[{"Paclet", "[", 
             RowBox[{
              RowBox[{"Name", " ", "\[Rule]", " ", "appName"}], "\n", 
              "      ", ",", " ", 
              RowBox[{"Version", " ", "\[Rule]", " ", "version"}], "\n", 
              "      ", ",", " ", 
              RowBox[{
              "MathematicaVersion", " ", "\[Rule]", " ", "\"\<8+\>\""}], "\n",
               "      ", ",", " ", 
              RowBox[{"Extensions", " ", "\[Rule]", "\n", "        ", 
               RowBox[{"{", " ", 
                RowBox[{
                 RowBox[{"{", " ", 
                  RowBox[{"\"\<Kernel\>\"", ",", " ", 
                   RowBox[{"\"\<Context\>\"", " ", "\[Rule]", " ", 
                    RowBox[{"contextNames", "[", 
                    RowBox[{"appName", ",", " ", "appsDir"}], "]"}]}]}], 
                  "}"}], "\n", "        ", ",", " ", 
                 RowBox[{"{", " ", 
                  RowBox[{"\"\<Documentation\>\"", ",", " ", 
                   RowBox[{
                   "Language", " ", "\[Rule]", " ", "\"\<English\>\""}], ",", 
                   " ", 
                   RowBox[{"LinkBase", " ", "\[Rule]", " ", "appName"}], "\n",
                    "          ", ",", " ", 
                   RowBox[{
                   "Resources", " ", "\[Rule]", " ", "engResources"}]}], 
                  "}"}]}], "}"}]}]}], "]"}], "\n", "  ", ",", " ", 
            "\"\<Package\>\""}], "]"}]}], "]"}], ";", "\n", "\n", "  ", 
        RowBox[{
         RowBox[{
          RowBox[{"ChangeNotebookSettings", "[", "\n", "    ", 
           RowBox[{
            RowBox[{"FileNameJoin", "@", 
             RowBox[{"{", 
              RowBox[{
              "appNameDir", ",", " ", "\"\<Documentation\>\"", ",", " ", 
               "\"\<English\>\"", ",", " ", 
               RowBox[{"#", " ", "<>", " ", "\"\<.nb\>\""}]}], "}"}]}], "\n", 
            "  ", ",", " ", "index", ",", " ", "header", ",", " ", "footer"}],
            "]"}], "&"}], " ", "/@", "\n", "  ", "engResources"}], ";", "\n", 
        "\n", "  ", 
        RowBox[{
        "DocumentationSearch`CloseDocumentationNotebookIndexer", "@", 
         "index"}], ";", "\n", "  ", 
        RowBox[{"PacletManager`RestartPacletManager", "[", "]"}], ";"}]}], 
      "]"}]}], "]"}]}], "\n"}], "\n", 
 RowBox[{
  RowBox[{"BuildApplication", "@", "smthElse___"}], " ", ":=", " ", 
  RowBox[{"(", 
   RowBox[{
    RowBox[{"Message", "[", 
     RowBox[{"BuildApplication", "::", "argerr"}], "]"}], ";", "$Failed"}], 
   ")"}]}]}], "Code"]
}, Open  ]],

Cell[CellGroupData[{

Cell["DeployApplication", "Subsubsection"],

Cell[BoxData[{
 RowBox[{
  RowBox[{
   RowBox[{"DeployApplication", "[", "\n", "  ", 
    RowBox[{
    "appName_String", "\n", ",", " ", "destDir_String", "\n", ",", " ", 
     RowBox[{"appDir_String:", " ", "$ApplicationsDirectory"}]}], "]"}], " ", 
   ":=", "\n", 
   RowBox[{"With", "[", "\n", "  ", 
    RowBox[{
     RowBox[{"{", " ", 
      RowBox[{
       RowBox[{"deploymentDirectory", " ", "=", " ", 
        RowBox[{"FileNameJoin", "@", 
         RowBox[{"{", 
          RowBox[{"destDir", ",", " ", "appName"}], "}"}]}]}], "\n", "  ", 
       ",", "          ", 
       RowBox[{"appNameDir", " ", "=", " ", 
        RowBox[{"FileNameJoin", "@", 
         RowBox[{"{", " ", 
          RowBox[{"appDir", ",", " ", "appName"}], "}"}]}]}]}], " ", "}"}], 
     ",", "\n", 
     RowBox[{
      RowBox[{"If", "[", 
       RowBox[{
        RowBox[{"!", 
         RowBox[{"DirectoryQ", "@", "appNameDir"}]}], "\n", ",", "  ", 
        RowBox[{
         RowBox[{"Message", "[", 
          RowBox[{
           RowBox[{"BuildApplication", "::", "nodir"}], ",", " ", "appName", 
           ",", " ", "appDir"}], "]"}], ";", " ", 
         RowBox[{"Return", "@", "$Failed"}]}]}], "]"}], ";", "\n", "\n", 
      RowBox[{"If", "[", 
       RowBox[{
        RowBox[{"MatchQ", "[", 
         RowBox[{
          RowBox[{"CopyDirectory", "[", 
           RowBox[{"appNameDir", ",", " ", "deploymentDirectory"}], "]"}], 
          ",", " ", "$Failed"}], "]"}], "\n", ",", "  ", 
        RowBox[{"Return", "@", "$Failed"}]}], "]"}], ";", "\n", "\n", 
      RowBox[{
       RowBox[{
        RowBox[{
         RowBox[{"appName", "[", "\n", "  ", 
          RowBox[{"\"\<*.nb\>\"", "\n", ",", " ", 
           RowBox[{"\"\<Documentation\>\"", "[", "\n", "    ", 
            RowBox[{"\"\<English\>\"", "[", "\n", "      ", 
             RowBox[{
              RowBox[{"\"\<ReferencePages\>\"", "@", 
               RowBox[{"\"\<Symbols\>\"", "@", "#"}]}], "\n", "    ", ",", 
              " ", 
              RowBox[{"\"\<Guides\>\"", "@", "#"}], "\n", "    ", ",", " ", 
              RowBox[{"\"\<Tutorials\>\"", "@", "#"}]}], "]"}], "]"}]}], 
          "]"}], "&"}], " ", "@", " ", "\"\<___*.nb\>\""}], " ", "//", " ", 
       RowBox[{
        RowBox[{"deleteFiles", "[", 
         RowBox[{"#", ",", " ", "destDir"}], "]"}], "&"}]}], " ", ";", "\n", 
      "\n", 
      RowBox[{"Print", "[", "\n", "      ", 
       RowBox[{
        RowBox[{"plainMsg", " ", "@", " ", "\"\<The application \>\""}], ",", 
        " ", "\n", "       ", 
        RowBox[{"boldMsg", " ", "@", " ", "appName"}], ",", " ", "\n", 
        "      ", 
        RowBox[{"plainMsg", " ", "@", " ", "\"\< has been deployed to \>\""}],
         ",", " ", "\n", "       ", 
        RowBox[{"boldMsg", " ", "@", " ", "deploymentDirectory"}]}], "]"}], 
      ";"}]}], "]"}]}], "\n"}], "\n", 
 RowBox[{
  RowBox[{"DeployApplication", "@", "smthElse___"}], " ", ":=", " ", 
  RowBox[{"(", 
   RowBox[{
    RowBox[{"Message", "[", 
     RowBox[{"DeployApplication", "::", "argerr"}], "]"}], ";", " ", 
    "$Failed"}], ")"}]}]}], "Code"]
}, Closed]]
}, Open  ]]
}, Open  ]],

Cell[CellGroupData[{

Cell["Finalization", "Section"],

Cell[BoxData[
 RowBox[{
  RowBox[{
   RowBox[{"End", "[", "]"}], ";"}], " ", 
  RowBox[{"(*", " ", "`Private`", " ", "*)"}]}]], "Code"],

Cell[BoxData[
 RowBox[{
  RowBox[{"Protect", "[", 
   RowBox[{
   "NewApplication", ",", " ", "BuildApplication", ",", " ", 
    "DeployApplication"}], "]"}], ";"}]], "Code"],

Cell[BoxData[
 RowBox[{
  RowBox[{
   RowBox[{"EndPackage", "[", "]"}], ";"}], " ", 
  RowBox[{"(*", " ", "ApplicationMaker`", " ", "*)"}]}]], "Code"]
}, Closed]]
},
WindowSize->{714, 563},
WindowMargins->{{124, Automatic}, {41, Automatic}},
PrivateNotebookOptions->{"FileOutlineCache"->False},
FrontEndVersion->"8.0 for Microsoft Windows (64-bit) (February 23, 2011)",
StyleDefinitions->"Default.nb",
$CellContext`TrackCellChangeTimes -> False
]

